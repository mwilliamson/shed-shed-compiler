module shed/compiler/parsing/expressions;

members {
    expressionRules
}

import lists
import sequences

import lop/rules;
import lop/pratt;

import shed/compiler/nodes;
import shed/compiler/nodes.NumberNode;
import shed/compiler/nodes.UnitNode;
import shed/compiler/nodes.BooleanNode;

import shed/compiler/parsing/tokenRules;
import shed/compiler/parsing/literals;

val variableReferenceRule = rules.map(
    tokenRules.identifier(),
    nodes.variableReference
);
    
def expressionRules fun(statementRule: Rule[StatementNode]) => do {
    val ifThenElseRule = lazyFunction(fun() =>
        rules.sequence()
            .next(tokenRules.keyword("if"))
            .cut()
            .capture(expressionRule)
            .next(tokenRules.keyword("then"))
            .capture(expressionRule)
            .next(rules.optional(tokenRules.newline()))
            .next(tokenRules.keyword("else"))
            .capture(expressionRule)
            .map(nodes.ifThenElse)
    );

    val bracketedRule = lazyFunction(fun() =>
        rules.sequence()
            .next(tokenRules.symbol("("))
            .capture(expressionRule)
            .cut()
            .next(tokenRules.closingParen())
            .head()
    );

    val functionRule = lazyFunction(fun() =>
        rules.sequence()
            .next(tokenRules.keyword("fun"))
            .cut()
            .next(rules.optional(formalTypeParametersRule))
            .capture(formalArgumentListRule)
            .next(rules.optional(typeSpecifierRule))
            .next(tokenRules.symbol("=>"))
            .capture(expressionRule)
            .map(nodes.func)
    );

    val formalTypeParametersRule = lazyFunction(fun() =>
        rules.sequence()
            .next(tokenRules.symbol("["))
            .cut()
            .next(rules.zeroOrMoreWithSeparator(formalParameterRule, tokenRules.symbol(",")))
            .next(tokenRules.symbol("]"))
            .next(tokenRules.symbol("=>"))
            .tuple()
    );
    
    val formalParameterRule = lazyFunction(fun() =>
        expressionRule
    );

    val formalArgumentListRule = lazyFunction(fun() =>
        rules.sequence()
            .next(tokenRules.symbol("("))
            .capture(rules.zeroOrMoreWithSeparator(formalArgumentRule, tokenRules.symbol(",")))
            .next(tokenRules.closingParen())
            .head()
    );
            
    val formalArgumentRule = lazyFunction(fun() =>
        rules.sequence()
            .capture(tokenRules.identifier())
            .cut()
            .capture(typeSpecifierRule)
            .map(nodes.formalArgument)
    );
    
    val typeSpecifierRule = lazyFunction(fun() =>
        rules.sequence()
            .next(tokenRules.symbol(":"))
            .cut()
            .capture(expressionRule)
            .head()
    );

    val memberDeclarationRule = lazyFunction(fun() =>
        rules.sequence()
            .capture(tokenRules.identifier())
            .capture(rules.optional(expressionRule))
            .map(fun(name: String, value: Option[ExpressionNode]) =>
                nodes.memberDeclaration(name, value.valueOrElse(fun() => nodes.ref(name)))
            )
    );

    val memberSeparator = tokenRules.symbolOrNewline(",")

    val membersRule =
        rules.sequence()
            .next(tokenRules.keyword("members"))
            .cut()
            .next(tokenRules.openingBrace())
            .capture(rules.zeroOrMoreWithSeparator(memberDeclarationRule, memberSeparator))
            .next(rules.optional(memberSeparator))
            .next(tokenRules.symbol("}"))
            .next(rules.optional(tokenRules.statementTerminator))
            .head();

    val optionalMembersRule =
        rules.map(
            rules.optional(membersRule),
            fun(value: Option[List[MemberDeclarationNode]]) =>
                value.valueOrElse(fun() => emptyList)
        );
    
    val objectBodyRule =
        rules.sequence()
            .next(tokenRules.openingBrace())
            .capture(optionalMembersRule)
            .capture(rules.zeroOrMore(statementRule))
            .next(tokenRules.symbol("}"))
            .map(nodes.obj);
    
    val classRule =
        rules.sequence()
            .next(tokenRules.keyword("class"))
            .cut()
            .next(rules.optional(formalTypeParametersRule))
            .capture(formalArgumentListRule)
            .next(tokenRules.symbol("=>"))
            .capture(objectBodyRule)
            .map(nodes.classDeclaration);
    
    val objectRule =
        rules.sequence()
            .next(tokenRules.keyword("object"))
            .cut()
            .capture(objectBodyRule)
            .head();

    val doBlockRule =
        rules.sequence()
            .next(tokenRules.keyword("do"))
            .cut()
            .next(tokenRules.openingBrace())
            .capture(rules.zeroOrMore(statementRule))
            .next(tokenRules.symbol("}"))
            .map(nodes.doBlock);

    val letDeclarationRule = lazyFunction(fun() =>
        rules.sequence()
            .next(tokenRules.keyword("val"))
            .cut()
            .capture(tokenRules.identifier())
            .next(tokenRules.symbol("="))
            .capture(expressionRule)
            .map(nodes.valDeclaration)
    );

    val letInRule = lazyFunction(fun() =>
        rules.sequence()
            .next(tokenRules.keyword("let"))
            .cut()
            .capture(rules.zeroOrMore(letDeclarationRule))
            .next(tokenRules.keyword("in"))
            .capture(expressionRule)
            .map(nodes.letIn)
    );
    
    val implicitLetInRule = lazyFunction(fun() => let
        val valRule = rules.sequence()
            .capture(letDeclarationRule)
            .next(tokenRules.statementTerminator)
            .head()
        
        // TODO: implement oneOrMore rule
        
        in rules.sequence()
            .capture(valRule)
            .cut()
            .capture(rules.zeroOrMore(valRule))
            .capture(expressionRule)
            .map(fun(firstVal: _, furtherVals: _, expression: _) =>
                let val vals = lists.sequenceToList(sequences.cons(firstVal, furtherVals))
                in nodes.letIn(vals, expression)
            )
    );

    val primaryExpressionRule = rules.firstOf("primary expression",
        listOf(
            literals.literalRule,
            variableReferenceRule,
            ifThenElseRule,
            bracketedRule,
            functionRule,
            objectRule,
            classRule,
            doBlockRule,
            letInRule,
            implicitLetInRule
        )
    );
        
    val partialCallRule = lazyFunction(fun() =>
        rules.sequence()
            .next(tokenRules.symbol("("))
            .cut()
            .capture(rules.zeroOrMoreWithSeparator(expressionRule, tokenRules.symbol(",")))
            .next(tokenRules.closingParen())
            .map(fun(args: List[ExpressionNode]) =>
                fun(left: ExpressionNode) => nodes.call(left, args))
    );
    
    val partialTypeApplicationRule = lazyFunction(fun() =>
        rules.sequence()
            .next(tokenRules.symbol("["))
            .cut()
            .next(rules.zeroOrMoreWithSeparator(expressionRule, tokenRules.symbol(",")))
            .next(tokenRules.symbol("]"))
            .map(fun() => fun(left: ExpressionNode) => left)
    );
        
    val partialMemberAccessRule = lazyFunction(fun() =>
        rules.sequence()
            .next(tokenRules.symbol("."))
            .cut()
            .capture(tokenRules.identifier())
            .map(fun(memberName: String) =>
                fun(left: ExpressionNode) => nodes.memberAccess(left, memberName))
    );
    
    val partialAndRule = lazyFunction(fun() =>
        rules.sequence()
            .next(tokenRules.symbol("&&"))
            .capture(expressionParser.leftAssociative("and"))
            .map(fun(right: ExpressionNode) => fun(left: ExpressionNode) =>
                nodes.and(left, right)
            )
    );

    val expressionParser = pratt.parser("expression",
        listOf(primaryExpressionRule),
        listOf(
            pratt.infix("call", partialCallRule),
            pratt.infix("typeApplication", partialTypeApplicationRule),
            pratt.infix("memberAccess", partialMemberAccessRule),
            pratt.infix("and", partialAndRule)
        )
    );

    val expressionRule = expressionParser.rule();
    return object {
        members {
            expression fun() => expressionRule,
            membersRule fun() => membersRule,
            optionalMembers fun() => optionalMembersRule
        }
    };  
}
