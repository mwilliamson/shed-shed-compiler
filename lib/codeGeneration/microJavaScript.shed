module shed.compiler.codeGeneration.microJavaScript;

members {
    generate,
    Generator
}

import shed.compiler.nodes;
import shed.compiler.javaScript.js;

val shed = nodes;
    
def generate fun(node: Node) =>
    Generator(generate).generate(node)

def Generator class(generate: Function[ShedNode, JavaScriptNode]) => {
    members {
        generate generatorGenerate
    }
    
    def generatorGenerate fun(node: ShedNode) =>
        match(node,
            matchClass(shed.UnitNode, unit),
            matchClass(shed.BooleanNode, boolean),
            matchClass(shed.StringNode, string),
            matchClass(shed.VariableReferenceNode, variableReference),
            matchClass(shed.CallNode, call),
            matchClass(shed.ExpressionStatementNode, expressionStatement),
            matchClass(shed.ModuleNode, moduleNode)
        )

    def unit fun(unit: UnitNode) =>
        js.propertyAccess(js.ref("$shed"), "unit")

    def boolean fun(boolean: BooleanNode) =>
        js.boolean(boolean.value())

    def string fun(string: StringNode) =>
        js.string(string.value())

    def variableReference fun(variableReference: VariableReferenceNode) =>
        js.ref(variableReference.identifier())
        
    def call fun(call: CallNode) =>
        js.call(generate(call.callee()), call.args().map(generate))

    def expressionStatement fun(expressionStatement: ExpressionStatementNode) =>
        js.expressionStatement(generate(expressionStatement.expression()))
        
    def moduleNode fun(moduleNode: ModuleNode) =>
        js.statements(moduleNode.statements().map(generate))
}
