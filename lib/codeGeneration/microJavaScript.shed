module shed/compiler/codeGeneration/microJavaScript;

members::
    generate,
    Generator

import strings;
import lists;
import sequences;
import lazySequences;

import shed/compiler/nodes;
import shed/compiler/javaScript/js;

val shed = nodes;
    
def generate fun(node: Node) =>
    Generator(generate).generate(node)

def Generator class(generate: Function[ShedNode, JavaScriptNode]) => ::
    members::
        generate generatorGenerate
    
    val shedGlobal = js.ref("$shed");
    
    def generatorGenerate fun(node: ShedNode) =>
        match(node,
            matchClass(shed.UnitNode, unit),
            matchClass(shed.BooleanNode, bool),
            matchClass(shed.NumberNode, number),
            matchClass(shed.StringNode, string),
            matchClass(shed.VariableReferenceNode, variableReference),
            matchClass(shed.IfThenElseNode, ifThenElse),
            matchClass(shed.CallNode, call),
            matchClass(shed.TypeApplicationNode, typeApplication),
            matchClass(shed.MemberAccessNode, memberAccess),
            matchClass(shed.FunctionNode, func),
            matchClass(shed.ClassNode, classDeclaration),
            matchClass(shed.ObjectNode, obj),
            matchClass(shed.DoBlockNode, doBlock),
            matchClass(shed.LetInNode, letIn),
            matchClass(shed.AndNode, and),
            
            matchClass(shed.ExpressionStatementNode, expressionStatement),
            matchClass(shed.ReturnStatementNode, returnStatement),
            matchClass(shed.ValDeclarationNode, valDeclaration),
            matchClass(shed.DefinitionNode, definition),
            
            matchClass(shed.ImportNode, importNode),
            matchClass(shed.ModuleNode, moduleNode)
        )

    def unit fun(unit: UnitNode) =>
        js.propertyAccess(shedGlobal, "unit")

    def bool fun(bool: BooleanNode) =>
        js.bool(bool.value())

    def number fun(number: NumberNode) =>
        js.call(
            js.propertyAccess(shedGlobal, "number"),
            listOf(js.number(number.value()))
        )

    def string fun(string: StringNode) =>
        js.call(
            js.propertyAccess(shedGlobal, "string"),
            listOf(js.string(string.value()))
        )

    def variableReference fun(variableReference: VariableReferenceNode) =>
        js.ref(variableReference.identifier())
    
    def ifThenElse fun(ifThenElse: IfThenElseNode) =>
        js.conditional(
            generate(ifThenElse.condition()),
            generate(ifThenElse.trueValue()),
            generate(ifThenElse.falseValue())
        )
    
    def call fun(call: CallNode) =>
        js.call(
            if classOf(call.callee()).equals(nodes.MemberAccessNode) then
                js.propertyAccess(generate(call.callee().left()), call.callee().memberName())
            else
                generate(call.callee()),
            call.args().map(generate)
        )
    
    def typeApplication fun(typeApplication: TypeApplicationNode) =>
        generate(typeApplication.callee())
    
    def memberAccess fun(memberAccess: MemberAccessNode) =>
        js.call(
            js.propertyAccess(js.ref("$shed"), "memberAccess"),
            listOf(
                generate(memberAccess.left()),
                js.propertyAccess(generate(memberAccess.left()), memberAccess.memberName())
            )
        )

    def func fun(func: FunctionNode) =>
        js.call(
            js.propertyAccess(shedGlobal, "function"),
            listOf(js.func(
                generateFormalArgs(func.formalInputs()),
                listOf(js.ret(generate(func.body())))
            ))
        )
        
    def classDeclaration fun(classDeclaration: ClassNode) =>
        js.call(
            js.func(
                emptyList,
                listOf(
                    js.varDeclaration("$class", js.call(
                        js.propertyAccess(shedGlobal, "class"),
                        listOf(js.func(
                            generateFormalArgs(classDeclaration.formalInputs()),
                            generateObjectBody(classDeclaration.body())
                        ))
                    )),
                    js.ret(js.ref("$class"))
                )
            ),
            emptyList
        )
        
        
        
    def obj fun(obj: ObjectNode) =>
        js.call(
            generate(shed.classDeclaration(nodes.formalInputs(emptyList), obj)),
            emptyList
        )

    def doBlock fun(doBlock: DoBlockNode) =>
        js.call(
            js.func(
                emptyList,
                doBlock.statements().map(generate)  
            ),
            emptyList
        )
    
    def letIn fun(letIn: LetInNode) => let
        val jsDeclarations = letIn.declarations().map(generate)
        val jsExpression = generate(letIn.expression())
        in js.call(
            js.func(
                emptyList,
                jsDeclarations.concat(listOf(js.ret(jsExpression)))
            ),
            emptyList
        )
    
    def and fun(andNode: AndNode) =>
        js.and(
            generate(andNode.left()),
            generate(andNode.right())
        )

    def expressionStatement fun(expressionStatement: ExpressionStatementNode) =>
        js.expressionStatement(generate(expressionStatement.expression()))
    
    def returnStatement fun(returnStatement: ReturnStatementNode) =>
        js.ret(generate(returnStatement.value()))
        
    def valDeclaration fun(valDeclaration: ValDeclarationNode) =>
        js.varDeclaration(
            valDeclaration.name(),
            generate(valDeclaration.value())
        )
    
    def definition fun(definition: DefinitionNode) =>
        js.varDeclaration(
            definition.name(),
            js.call(
                js.propertyAccess(generate(definition.value()), "$define"),
                listOf(js.string(definition.name()))
            )
        )
    
    def importNode fun(importNode: ImportNode) => let
        val moduleExpression = js.call(
            js.propertyAccess(
                js.propertyAccess(
                    js.ref("$shed"),
                    "js"
                ),
                "import"
            ),
            listOf(js.string(strings.join(".", importNode.moduleName())))
        )
        
        in js.varDeclaration(
            importNode.name(),
            importNode.valuePath().foldLeft(moduleExpression, js.propertyAccess)
        )
    
    def moduleNode fun(moduleNode: ModuleNode) =>
        moduleNode.name()
            .map(fun(name: List[String]) => generateNamedModule(name, moduleNode))
            .valueOrElse(fun() => generateUnnamedModule(moduleNode))

    def generateFormalArgs fun(formalInputs: FormalInputsNode) =>
        formalInputs.args().map(fun(arg) => arg.name())

    def generateObjectBody fun(body: ObjectNode) =>
        body.statements().map(generate)
            .concat(listOf(js.ret(generateMembersObject(body.memberDeclarations()))))
            
    def generateMembersObject fun(membersDeclaration: List[MemberDeclarationNode]) =>
        js.obj(lists.sequenceToList(sequences.cons(
            tuple("$class", js.ref("$class")),
            generateMembers(membersDeclaration)
        )))
        
    def generateMembers fun(membersDeclaration: List[MemberDeclarationNode]) =>
        lazySequences.map(
            fun(memberDeclaration: MemberDeclarationNode) =>
                tuple(memberDeclaration.name(), generate(memberDeclaration.value())),
            membersDeclaration
        )
        
    def generateNamedModule fun(name: List[String], moduleNode: ModuleNode) => let
        val membersObject = js.obj(lists.sequenceToList(
            generateMembers(moduleNode.memberDeclarations())
        ))
        in js.expressionStatement(
            js.call(
                js.propertyAccess(shedGlobal, "exportModule"),
                listOf(
                    js.string(strings.join(".", name)),
                    js.func(
                        emptyList,
                        moduleNode.statements().map(generate)
                            .concat(listOf(js.ret(membersObject)))
                    )
                )
            )
        );
    
    def generateUnnamedModule fun(moduleNode: ModuleNode) =>
        js.call(
            js.func(
                emptyList,
                moduleNode.statements().map(generate)
            ),
            emptyList
        )
